CMAKE_MINIMUM_REQUIRED(VERSION 2.6)

PROJECT(CiftiLib_and_example)

SET(CMAKE_CXX_FLAGS "-W -Wall -fPIC")

SET(IGNORE_QT FALSE CACHE BOOL "don't try to use QT")

SET(CIFTI_DEFINITIONS)

#
# Use QT for xml/string
#
IF (NOT IGNORE_QT)
    FIND_PACKAGE(Qt4 4.8)
    IF (QT_FOUND)
        SET(QT_DONT_USE_QTGUI TRUE)
        SET(CIFTILIB_USE_QT TRUE)
        ADD_DEFINITIONS(-DCIFTILIB_USE_QT)
        LIST(APPEND CIFTI_DEFINITIONS "-DCIFTILIB_USE_QT")
        INCLUDE(${QT_USE_FILE})
        SET(LIBS ${LIBS} ${QT_LIBRARIES})
    ENDIF (QT_FOUND)
ENDIF (NOT IGNORE_QT)

IF (NOT QT_FOUND)
    SET(CMAKE_MODULE_PATH ${CMAKE_MODULE_PATH} "${CMAKE_SOURCE_DIR}/cmake/Modules/")
    FIND_PACKAGE(libxml++ REQUIRED)
    INCLUDE_DIRECTORIES(${libxml++_INCLUDE_DIRS})
    SET(LIBS ${LIBS} ${libxml++_LIBRARIES})
    ADD_DEFINITIONS(-DCIFTILIB_USE_XMLPP)
    LIST(APPEND CIFTI_DEFINITIONS "-DCIFTILIB_USE_XMLPP")
ENDIF (NOT QT_FOUND)

IF (NOT QT_FOUND)
    FIND_PACKAGE(Boost REQUIRED COMPONENTS filesystem system)
ELSE (NOT QT_FOUND)
    FIND_PACKAGE(Boost REQUIRED)
ENDIF (NOT QT_FOUND)
SET(LIBS ${LIBS} ${Boost_LIBRARIES})
IF (Boost_VERSION LESS 104400)
    #absolute() was added in 1.44.0, with filesystem v3
    ADD_DEFINITIONS(-DCIFTILIB_BOOST_NO_FSV3)
    LIST(APPEND CIFTI_DEFINITIONS "-DCIFTILIB_BOOST_NO_FSV3")
ENDIF (Boost_VERSION LESS 104400)
IF (Boost_VERSION LESS 104800)
    #canonical() was added in 1.48.0
    ADD_DEFINITIONS(-DCIFTILIB_BOOST_NO_CANONICAL)
    LIST(APPEND CIFTI_DEFINITIONS "-DCIFTILIB_BOOST_NO_CANONICAL")
ENDIF (Boost_VERSION LESS 104800)
IF (Boost_VERSION LESS 105600)
    #try_lexical_cast was added in 1.56.0
    ADD_DEFINITIONS(-DCIFTILIB_BOOST_NO_TRY_LEXICAL)
    LIST(APPEND CIFTI_DEFINITIONS "-DCIFTILIB_BOOST_NO_TRY_LEXICAL")
ENDIF (Boost_VERSION LESS 105600)

FIND_PACKAGE(ZLIB)
IF (ZLIB_FOUND)
    ADD_DEFINITIONS("-DCARET_HAVE_ZLIB")
    LIST(APPEND LIBS ${ZLIB_LIBRARIES})
    LIST(APPEND CIFTI_DEFINITIONS "-DCARET_HAVE_ZLIB")
ENDIF (ZLIB_FOUND)

#OS X has some weirdness in its zlib, so let the preprocessor know
IF (APPLE)
    ADD_DEFINITIONS(-DCARET_OS_MACOSX)
    LIST(APPEND CIFTI_DEFINITIONS "-DCARET_OS_MACOSX")
ENDIF (APPLE)

SET(CIFTI_LIBS ${LIBS} PARENT_SCOPE)
SET(CIFTI_DEFINITIONS ${CIFTI_DEFINITIONS} PARENT_SCOPE)
SET(CIFTI_INCLUDES ${CMAKE_CURRENT_SOURCE_DIR}/Common
    ${CMAKE_CURRENT_SOURCE_DIR}/Nifti
    ${CMAKE_CURRENT_SOURCE_DIR}/Cifti
    PARENT_SCOPE)

SET(_SAVE_SOURCE_DIR ${CMAKE_SOURCE_DIR})
SET(CMAKE_SOURCE_DIR ${CMAKE_CURRENT_SOURCE_DIR})

ADD_SUBDIRECTORY(Common)
ADD_SUBDIRECTORY(Nifti)
ADD_SUBDIRECTORY(Cifti)
ADD_SUBDIRECTORY(example)

SET(CMAKE_SOURCE_DIR ${_SAVE_SOURCE_DIR})
